#!/usr/bin/env perl
use warnings;
use strict;
use File::Find qw(find);
use File::Spec::Functions qw(abs2rel rel2abs);
use Getopt::Long;

our $force = 0;
our $dry_run = 0;
our $verbose = 0;

Getopt::Long::Configure(qw());
Getopt::Long::GetOptions(
    'f|force'    => sub { $dry_run = 0; $force = 1; },
    'n|dry-run'  => sub { $dry_run = 1; $force = 0; },
    'v|verbose+' => \$verbose,
) or die(":-(\n");
if (!$force && !$dry_run) {
    die("either -f/--force or -n/--dry-run must be specified\n");
}

use FindBin;
use lib "${FindBin::Bin}/../lib";
use My::ShellQuote qw(shell_quote);
use My::Progress qw();

use File::Find qw(finddepth);

STDOUT->autoflush(1);
STDERR->autoflush(1);

# Keep track of relative pathnames found, so if we delete dir1/file1
# and dir3/file1 in an earlier round, we ignore file1 in later rounds.
my %found;

my @dirs = @ARGV;
while (scalar @dirs >= 2) {
    cleanup(@dirs);
    shift(@dirs);
}

sub cleanup {
    my ($base_dir, @other_dirs) = @_;
    my $prefix = "$base_dir/";
    my $prefixlen = length($prefix);
    warn(sprintf("# finding files in $base_dir and %d other places\n", scalar @other_dirs));
    my $progress = My::Progress->new(tick => 1);

    my $file_found_count = 0;
    my $file_checked_count = 0;
    my $file_removed_count = 0;
    my $sub = sub {
        $progress->printf("  %d files found, %d checked, %d removed",
                          $file_found_count,
                          $file_checked_count,
                          $file_removed_count);
    };

    finddepth({
        preprocess => sub { return sort @_; }, # predictable order
        wanted => sub {
            $file_found_count += 1;
            my ($dev, $ino, $mode, $nlink) = lstat($_);
            if (!defined $dev || !defined $ino || !-f _ || $nlink < 2) {
                &$sub();
                return;
            }
            my $rel = substr($File::Find::name, $prefixlen);
            if ($found{$rel}) {
                &$sub();
                return;
            }
            $found{$rel} = 1;
            $file_checked_count += 1;
            &$sub();
            foreach my $other_dir (@other_dirs) {
                my $abs = "$other_dir/$rel";
                my ($dev2, $ino2, $mode2, $nlink2) = lstat($abs);
                next if !defined $dev2 || !defined $ino2 || $nlink2 < 2;
                next if $dev2 != $dev || $ino2 != $ino;
                if ($force) {
                    $file_removed_count += 1;
                    if (unlink($abs)) {
                        $progress->clear();
                        warn("rm $abs # [5]\n") if $verbose;
                        &$sub();
                    } else {
                        $progress->clear();
                        warn("$abs: $!\n");
                        &$sub();
                    }
                } else {
                    $progress->clear();
                    printf("rm %s # [6]\n", shell_quote($abs));
                    &$sub();
                }
            }
        },
        no_chdir => 1,
    }, $base_dir);
}
