#!/usr/bin/env perl
use warnings;
use strict;
my %dev_ino_file;
while (<>) {
    s{\R\z}{};
    my ($ver, @fields) = split();
    my ($dev, $ino, $size, $mtime, $filename);
    if ($ver eq 'v1') {
        ($dev, $ino, $size, $mtime, $filename) = @fields;
    } else {
        next;
    }
    push(@{$dev_ino_file{"$dev,$ino"}}, $filename);
    #                     ^^^^^^^^^ gets printed
}
foreach my $dev_ino (keys %dev_ino_file) {
    my $filenames = delete $dev_ino_file{$dev_ino};
    next if scalar @$filenames < 2;
    my @filenames = sort @$filenames;
    my $main_filename = shift(@filenames);
    printf("## %s (%s)\n", shellquote($main_filename), $dev_ino);
    printf("rm %s\n", shellquote($_)) foreach @filenames;
}
sub shellquote {
    my ($string) = @_;
    for ($string) {
        # stolen from String::ShellQuote to reduce dependencies
        s/'/'\\''/g;
        s|((?:'\\''){2,})|q{'"} . (q{'} x (length($1) / 4)) . q{"'}|ge;
        $_ = "'$_'";
        s/^''//;
        s/''$//;
    }
    return $string;
}
