#!/usr/bin/env perl
use warnings;
use strict;
use File::Find qw(find);
use File::Spec::Functions qw(abs2rel rel2abs);
use Getopt::Long;
STDOUT->autoflush(1);
my @dirs = @ARGV;
#
# cleanup dir1 dir2 dir3
# cleanup dir2 dir3
#
while (scalar @dirs >= 2) {
    cleanup(@dirs);
    shift(@dirs);
}
sub cleanup {
    my ($base_dir, @other_dirs) = @_;
    my $file_counter = 0;
    my $prefix = "$base_dir/";
    my $prefixlen = length($prefix);
    find({
        wanted => sub {
            my ($dev, $ino, $mode, $nlink) = lstat($_);
            return if !defined $dev || !defined $ino || !-f _ || $nlink < 2;
            return if substr($File::Find::name, 0, $prefixlen) ne $prefix;
            my $rel = substr($File::Find::name, $prefixlen);
            my $keep = 0;
            foreach my $other_dir (@other_dirs) {
                my $abs = "$other_dir/$rel";
                my ($dev2, $ino2, $mode2, $nlink2) = lstat($abs);
                next if !defined $dev2 || !defined $ino2 || $nlink2 < 2;
                next if $dev2 != $dev || $ino2 != $ino;
                printf("## %s (%s,%s)\n", shellquote($abs), $dev, $ino) if !$keep++;
                printf("rm %s\n", shellquote($abs));
            }
        },
        no_chdir => 1,
    }, $base_dir);
}
sub shellquote {
    my ($string) = @_;
    for ($string) {
        # stolen from String::ShellQuote to reduce dependencies
        s/'/'\\''/g;
        s|((?:'\\''){2,})|q{'"} . (q{'} x (length($1) / 4)) . q{"'}|ge;
        $_ = "'$_'";
        s/^''//;
        s/''$//;
    }
    return $string;
}
